.include "nominal.jsim"
.include "stdcell.jsim"
.include "lab3_mult.jsim"
*.include "lab3_testadder.jsim"
*.include "lab3_testcompare.jsim"
*.include "lab3_testboolean.jsim"
*.include "lab3_testshifter.jsim"
*.include "lab3_testmultiply.jsim"
.include "lab3checkoff.jsim"

**********FA circuit***********
.subckt FA a b cin s co
*A xor B xor Cin
Xs1 a b s1 xor2

*xor gate with inputs A and B, outputs s1
Xs2 s1 cin s xor2

*******Carry out********
Xc1 a b co1 nand2
Xc2 a cin co2 nand2
Xc3 b cin co3 nand2
Xc4 co1 co2 co3 co nand3
.ends

***********Adder32 circuit**********
.subckt adder32 ALUFN0 a[31:0] b[31:0] s[31:0] z v n

Xxor_b b[31:0] alufn0#32 xb[31:0] xor2

Xbit0 a0 xb0 ALUFN0 s0 c0 FA
Xbit_therest a[31:1] xb[31:1] c[30:0] s[31:1] c[31:1] FA

*****z-value*******
Xlayer_1 s[31:16] s[15:0] layer_1[15:0] or2
Xlayer_2 layer_1[15:8] layer_1[7:0] layer_2[7:0] or2
Xlayer_3 layer_2[7:4] layer_2[3:0] layer_3[3:0] or2
Xlayer_4 layer_3[3:2] layer_3[1:0] layer_4[1:0] or2
Xz layer_40 layer_41 z nor2

******v-value (overflow)*******
Xnots31 s31 nots31 inverter
Xnotxb31 xb31 notxb31 inverter
Xnota31 a31 nota31 inverter
Xnegnegpos a31 xb31 nots31 negnegpos and3
Xposposneg nota31 notxb31 s31 posposneg and3
Xv_unit posposneg negnegpos v or2

Xbuffer s31 n buffer
.ends

***********Compare32 circuit*********
.subckt compare32 ALUFN[2:1] z v n cmp[31:0]

Xzero cmp[31:1] constant0
Xxor n v zxor xor2
Xxor_or zxor z z2 or2 
Xmux ALUFN2 ALUFN1 0 zxor z z2 cmp0 mux4
.ends

********Boolean32 circuit*******
.subckt boole32 ALUFN[3:0] A[31:0] B[31:0] boole[31:0]

Xboolean A[31:0] B[31:0] ALUFN0#32 ALUFN1#32 ALUFN2#32 ALUFN3#32 boole[31:0] mux4
.ends

*************Shifter32 circuit************
.subckt shift32 ALUFN[1:0] A[31:0] B[4:0] shift[31:0]
Xshift1 A[31:0] B[4:0] SHL[31:0] SHL
Xshift2 A[31:0] B[4:0] SHR[31:0] SHR
Xshift3 A[31:0] B[4:0] SRA[31:0] SRA

Xshift ALUFN1#32 ALUFN0#32 SHL[31:0] 0#32 SHR[31:0] SRA[31:0] shift[31:0] mux4
.ends

****SHL******
.subckt SHL A[31:0] B[4:0] SHL[31:0] 
Xb4 B4#32 A[31:0] A[15:0] 0#16 W[31:0] mux2
Xb3 B3#32 W[31:0] W[23:0] 0#8 X[31:0] mux2
Xb2 B2#32 X[31:0] X[27:0] 0#4 Y[31:0] mux2
Xb1 B1#32 Y[31:0] Y[29:0] 0#2 Z[31:0] mux2
Xb0 B0#32 Z[31:0] Z[30:0] 0 SHL[31:0] mux2
.ends

******SHR******
.subckt SHR A[31:0] B[4:0] SHR[31:0]
Xb4 B4#32 A[31:0] 0#16 A[31:16] W[31:0] mux2
Xb3 B3#32 W[31:0] 0#8 W[31:8] X[31:0] mux2
Xb2 B2#32 X[31:0] 0#4 X[31:4] Y[31:0] mux2
Xb1 B1#32 Y[31:0] 0#2 Y[31:2] Z[31:0] mux2
Xb0 B0#32 Z[31:0] 0 Z[31:1] SHR[31:0] mux2
.ends

****SRA*****
.subckt SRA A[31:0] B[4:0] SRA[31:0]
Xb4 B4#32 A[31:0] A31#16 A[31:16] W[31:0] mux2
Xb3 B3#32 W[31:0] A31#8 W[31:8] X[31:0] mux2
Xb2 B2#32 X[31:0] A31#4 X[31:4] Y[31:0] mux2
Xb1 B1#32 Y[31:0] A31#2 Y[31:2] Z[31:0] mux2
Xb0 B0#32 Z[31:0] A31 Z[31:1] SRA[31:0] mux2
.ends


**** ALU32 circuit ***************
.subckt alu ALUFN[5:0] A[31:0] B[31:0] alu[31:0] z v n

Xs ALUFN0 A[31:0] B[31:0] S[31:0] z v n adder32
Xmul A[31:0] B[31:0] MUL[31:0] mult32
Xbool ALUFN[3:0] A[31:0] B[31:0] bool[31:0] boole32
Xshift ALUFN[1:0] A[31:0] B[4:0] shift[31:0] shift32
Xcompare ALUFN[2:1] z v n cmp[31:0] compare32

X00 ALUFN1#32 S[31:0] MUL[31:0] X[31:0] mux2

Xalu ALUFN5#32 ALUFN4#32 X[31:0] shift[31:0] bool[31:0] 0#31 cmp0 alu[31:0] mux4

.ends